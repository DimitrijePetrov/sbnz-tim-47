template header
futureHours

package template;

import com.weatherforecast.model.MeasuredHourlyWeatherData;
import com.weatherforecast.model.PredictedHourlyTemperature;
import java.time.LocalDate;
import java.time.LocalTime;
import java.time.LocalDateTime;

template "temperatureTemplate"

rule "calculate base temperature @{futureHours} into the future"
salience 10
    when
        $m1: MeasuredHourlyWeatherData(
            date == LocalDateTime.now().plusHours(@{futureHours}).minusDays(1).toLocalDate(),
            hour == LocalTime.now().plusHours(@{futureHours}).getHour(),
            $t1: temperature
        )
        $m2: MeasuredHourlyWeatherData(
            date == LocalDateTime.now().plusHours(@{futureHours}).minusDays(2).toLocalDate(),
            hour == LocalTime.now().plusHours(@{futureHours}).getHour(),
            $t2: temperature
        )
        $m3: MeasuredHourlyWeatherData(
            date == LocalDateTime.now().plusHours(@{futureHours}).minusDays(3).toLocalDate(),
            hour == LocalTime.now().plusHours(@{futureHours}).getHour(),
            $t3: temperature
        )
        $average: Double() from (( $t1 * 1.6 + $t2 + $t3 * 0.4 ) / 3)
    then
        System.out.println("Base predicted temperature: " + $average + " from rule " + @{futureHours});
        insert(new PredictedHourlyTemperature(LocalDateTime.now().plusHours(@{futureHours}), $average));
end

rule "lower predicted temperature based on precipitation @{futureHours} into the future"
salience 9
    when
        $p: PredictedHourlyTemperature(
            date == LocalDateTime.now().plusHours(@{futureHours}).toLocalDate(),
            hour == LocalTime.now().plusHours(@{futureHours}).getHour(),
            precipitationApplied == false
        )
        $m1: MeasuredHourlyWeatherData(
            date == LocalDateTime.now().plusHours(@{futureHours}).minusDays(1).toLocalDate(),
            hour == LocalTime.now().plusHours(@{futureHours}).getHour(),
            precipitation > 1.0
        )
        $m2: MeasuredHourlyWeatherData(
            date == LocalDateTime.now().plusHours(@{futureHours}).minusDays(2).toLocalDate(),
            hour == LocalTime.now().plusHours(@{futureHours}).getHour(),
            precipitation == 0.0
        )
    then
        modify($p){
            applyPrecipitationFactor(0.9)
        }
        System.out.println("Temperature after precipitation: " + $p.getTemperature() + " from rule " + @{futureHours});
end

rule "update precipitation applied flag @{futureHours} into the future"
salience 8
    when
        $p: PredictedHourlyTemperature(
            date == LocalDateTime.now().plusHours(@{futureHours}).toLocalDate(),
            hour == LocalTime.now().plusHours(@{futureHours}).getHour(),
            precipitationApplied == false
        )
    then
        modify($p) {
            setPrecipitationApplied(true)
        }
        System.out.println("Temperature not modified by precipitation." + " from rule " + @{futureHours});
end

rule "detect rising temperature trend for @{futureHours} into the future"
salience 7
no-loop true
    when
        $p: PredictedHourlyTemperature(
            date == LocalDateTime.now().plusHours(@{futureHours}).toLocalDate(),
            hour == LocalTime.now().plusHours(@{futureHours}).getHour(),
            precipitationApplied == true
        )
        $m1: MeasuredHourlyWeatherData(
            date == LocalDateTime.now().plusHours(@{futureHours}).minusDays(1).toLocalDate(),
            hour == LocalTime.now().plusHours(@{futureHours}).getHour(),
            $t1: temperature
        )
        $m2: MeasuredHourlyWeatherData(
            date == LocalDateTime.now().plusHours(@{futureHours}).minusDays(2).toLocalDate(),
            hour == LocalTime.now().plusHours(@{futureHours}).getHour(),
            $t2: temperature < $t1
        )
        $m3: MeasuredHourlyWeatherData(
            date == LocalDateTime.now().plusHours(@{futureHours}).minusDays(3).toLocalDate(),
            hour == LocalTime.now().plusHours(@{futureHours}).getHour(),
            $t3: temperature < $t2
        )
        $trendModifier: Double() from ((( ($t1 - $t2) * 1.5 + ($t2 - $t3) * 0.5 ) / 2) * 1.5)
    then
        modify($p) {
            addTemperature($trendModifier)
        }
        System.out.println("Temperature after trend: " + $p.getTemperature() + " from rule " + @{futureHours});
end

rule "detect falling temperature trend for @{futureHours} into the future"
salience 6
no-loop true
    when
        $p: PredictedHourlyTemperature(
            date == LocalDateTime.now().plusHours(@{futureHours}).toLocalDate(),
            hour == LocalTime.now().plusHours(@{futureHours}).getHour(),
            precipitationApplied == true
        )
        $m1: MeasuredHourlyWeatherData(
            date == LocalDateTime.now().plusHours(@{futureHours}).minusDays(1).toLocalDate(),
            hour == LocalTime.now().plusHours(@{futureHours}).getHour(),
            $t1: temperature
        )
        $m2: MeasuredHourlyWeatherData(
            date == LocalDateTime.now().plusHours(@{futureHours}).minusDays(2).toLocalDate(),
            hour == LocalTime.now().plusHours(@{futureHours}).getHour(),
            $t2: temperature > $t1
        )
        $m3: MeasuredHourlyWeatherData(
            date == LocalDateTime.now().plusHours(@{futureHours}).minusDays(3).toLocalDate(),
            hour == LocalTime.now().plusHours(@{futureHours}).getHour(),
            $t3: temperature > $t2
        )
        $trendModifier: Double() from ((( ($t1 - $t2) * 1.5 + ($t2 - $t3) * 0.5 ) / 2) * 1.5)
    then
        modify($p) {
            addTemperature($trendModifier)
        }
        System.out.println("Temperature after trend: " + $p.getTemperature() + " from rule " + @{futureHours});
end

end template